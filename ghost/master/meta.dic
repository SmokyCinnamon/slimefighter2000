slimeNoise {
	if playSound {
		"\![sound,play,wav/slime1.wav,--rate=150]"
		"\![sound,play,wav/slime1.wav,--rate=175]"
		"\![sound,play,wav/slime1.wav,--rate=160]"
	}
}
enemyNoise {
	if playSound {
		case enemy[7] {
			when "6" {
				"\![sound,play,wav/guitar1.wav]"
				"\![sound,play,wav/guitar2.wav]"
				"\![sound,play,wav/guitar3.wav]"
			}
			when "21" {
				"\![sound,play,wav/ball1.wav]"
				"\![sound,play,wav/ball2.wav]"
				"\![sound,play,wav/ball3.wav,--rate=110]"
			}
			others {"\![sound,play,wav/ball3.wav,--rate=250]"}
		}
	}
}

OnShioriLoopLimit {
	20
}

//—————————————————————————————— установление ——————————————————————————————
OnInstallComplete
{
	"\0\s[0]установлен %(referencе0), по название %(reference1)."
	--
	if "ghost" _in_ reference0 //Меню, чтобы срочно сменить или вызывать привидение
	{
		"\n\n\_q"
		--
		"\![*]\__q[OnInstallComplete.Action,call]вызывать %(reference0)\__q  \![*]\__q[OnInstallComplete.Action,change]сменить на %(reference0)\__q\n\n"
		--
		"\![*]\q[спасибо,пустой]"
	}
}

OnInstallComplete.Action
{
	if reference0 == "change"
	{
		"\![change,ghost,lastinstalled]"
	}
	else //call
	{
		"\![call,ghost,lastinstalled]"
	}
}


//—————————————————————————————— создание .NAR ——————————————————————————————
OnNarCreated
{
	_nar = "\_a[OnNarLocation,""%(reference1)""]%(reference0)\_a" //создает ссылка, которая будет открыть место файл, который было только что создано
	
	"\0\s[0]успешно создано %(_nar)."
}

OnNarLocation
{	
	_path = SPLITPATH(reference0)[0] + SPLITPATH(reference0)[1]
	
	"\![open,file,""%(_path)""]"
}


//—————————————————————————————— обновление по сети ——————————————————————————————
//начало обновления
OnUpdateBegin
{
	"\0\s[0]проверка обновлении.\w8"
}

//reference0 - число новых файлов, начиная с 0
OnUpdateReady
{
	reference0++ //число новых файлов начинает с 0, так что это показывает правильное число.
	
	//Проверка многое число, добавыть 'и' если есть больше чем один файл (заметка от переводчик: есть способ показывать 'файлов' как надо, но это - легче. извините...)
	_и =""; if reference1 != 1; _и = "и"
	
	"\0\s[0]Есть %(reference0) файл%(и).\w8"
}

//когда закончить обновления, не забываите инициализировать новые переменние! Я очень рекомендую тебе использовать OnInitialize (в Boot.dіс), если бы пользователь обновили в меню искатель привидении или что-то.
//если нет новых файлов для обновлении, reference0 - 'none'. reference1 - список назвий новые файли расставаны с запятой
OnUpdateComplete
{
	if reference0 == "none"
	{
		"\0\s[0]нет новых файл."
	}
	else
	{
		"\0\s[0]успешно обновлен."
	}
}
//reference0 - причин обновление не было успешно
OnUpdateFailure //Если обновление не было успешно
{
	if reference0 == "timeout" //Если истекло время ожидания
	{
		"\0\s[0]истекло время ожидания."
	}
	elseif reference0 == "md5 miss" //Если файли были неправильно загрузено, а не совпадают с updates 2. dau
	{
		_файл = SPLITPATH(reference1)[2] + SPLITPATH(reference1)[3] //это узнает название и расширение у файла
		
		"\0\s[0]не совпадают значения MD5.\w8 сообщить автор привидении адайте им знать, что у '%(_файл)' есть ошибка MD5."
	}
	elseif reference0 == "artificial" //Если пользователь отменить обновление при двойнее нажатие поле с текстом
	{
		"\0\s[0]обновление отмено."
	}
	else //Другие ошибки
	{
		"\0\s[0]ошибка во время обновления:\w4 ""%(reference0)""."
	}
}

OnTranslate
{
	_talk = reference0
	
	//это то, что дозволит %(встроенные_элементи) работать в ввод скрипта
	if reference1 == "" && reference2 == "" //если это происходит от ввод скрипта
	{ //отправить вод скрипта : нет событии (ref2) , нет флаг (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	_telk = REPLACE(_talk,"\s[0]","\1\s[-1]\0\s[0]")
	if encounterRun; _talk = REPLACE(_talk,"\s[0]","\s[0]\i[%(enemy[7])]")
	
	TOSTR(_talk)
}

//это просто здесь, что ты можешь ссылковать веб-сайти как якорь, но обычные выбори меню ещё работает! Если так сделаешь, полезный просмотр будет в поле с текстом
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//горячие клавищи
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	elseif reference0 == "t"; OnAiTalk
	elseif reference0 == "r"; OnLastTalk
}

OnSurfaceRestore
{
	"\1\s[-1]\0\s[0]"
}

OnWindowStateRestore
{
	"\1\s[-1]\0\s[0]"
}

On_homeurl
{
	"https://raw.githubusercontent.com/SmokyCinnamon/slimefighter2000/main/"
}


//—————————————————————————————— ссылки меню нажатие правой кнопкой ——————————————————————————————

//форматировать массив попеременные названия и URL-и для использование с меню правой кнопки
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //добавить ссылка / название
		//переменнить с %ASC(1) до %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

recommendsites_sakura : array
{
	"Tamriel Balloon";	"https://zichqec.github.io/s-the-skeleton/balloons#tamriel"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//скопирован с файли SHIORI, с добавление переменная имя
OnNotifyUserInfo : void
{
}